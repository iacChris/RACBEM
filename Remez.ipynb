{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 18,
   "metadata": {},
   "outputs": [],
   "source": [
    "import Pkg\n",
    "# Pkg.add(\"GenericLinearAlgebra\")\n",
    "# Pkg.add(\"SpecialFunctions\")\n",
    "# Pkg.add(\"MAT\")\n",
    "# Pkg.add(\"Roots\")\n",
    "# Pkg.add(\"FFTW\")\n",
    "# Pkg.add(\"PyPlot\")\n",
    "# Pkg.add(\"PolynomialRoots\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GenericLinearAlgebra\n",
    "using LinearAlgebra\n",
    "using SpecialFunctions\n",
    "using Dates\n",
    "using MAT\n",
    "using Roots\n",
    "using FFTW\n",
    "using PyPlot\n",
    "using PolynomialRoots\n",
    "using Printf"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Remez (generic function with 6 methods)"
      ]
     },
     "execution_count": 20,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function Remez(targetf, parity, degree, xapp, lef = -1, rig = 1, maxiter = 10, sample_size = 20, eps = 1e-12)\n",
    "    #--------------------------------------------------------------------------------------------------------------\n",
    "    # Input:\n",
    "    #     targetf: target funciton\n",
    "    #     parity: the parity of approximation function, 0 -- even, 1 -- odd, 2 -- no constraint\n",
    "    #     degree: the degree of approximation polynomial\n",
    "    #     xapp: initial points to approximate, if provide a single zero, will choose roots of Chebyshev polynomials\n",
    "    #     lef, rig: left and right endpoints of approximation interval\n",
    "    #     maxiter: Max iteration \n",
    "    #     sample_size: sample size used in Remez algorithm\n",
    "    #     eps: accuracy of approximation\n",
    "    #\n",
    "    # Output:\n",
    "    #     Expansion of approximation polynomial under Chebyshev basis\n",
    "    #\n",
    "    # We note that our implementation does not strictly follow the reference. The algorithm may crush when \n",
    "    # the degree is very large or the problem is ill-conditioned.\n",
    "    #\n",
    "    #------------------------------------------------------------------------------------------------------------\n",
    "    #\n",
    "    # Reference:\n",
    "    #     E. W. Cheney.\n",
    "    #     Introduction to approximation theory\n",
    "    #\n",
    "    # Author: X. Meng\n",
    "    # Version 1.0 .... 02/2020\n",
    "    #\n",
    "    #------------------------------------------------------------------------------------------------------------\n",
    "    \n",
    "    if(xapp==0)\n",
    "        xapp = big.(cos.(collect(range(pi/2,stop=0,length=degree+1))))\n",
    "    end\n",
    "    iter = 0\n",
    "    eps2 = 1e-18\n",
    "    comp = ones(degree+1,1)\n",
    "    comp[2:2:end] .= -1\n",
    "    while(true)\n",
    "        iter += 1\n",
    "        \n",
    "        # Step1: Find the best approximation on n+1 given points by solving a linear equation\n",
    "        \n",
    "        A = zeros(BigFloat,degree+1,degree+1)\n",
    "        b = zeros(BigFloat,degree+1,1)\n",
    "        for i=1:degree\n",
    "            if(parity==0)\n",
    "                deg = 2*(i-1)\n",
    "            elseif(parity==1)\n",
    "                deg = 2*i-1\n",
    "            else\n",
    "                deg = i-1\n",
    "            end\n",
    "            for j=1:degree+1\n",
    "                A[j,i] = chebyshev(xapp[j],deg)\n",
    "            end\n",
    "        end\n",
    "        A[:,end] = comp\n",
    "        for i=1:degree+1\n",
    "            b[i] = targetf(xapp[i])\n",
    "        end\n",
    "        sol = A\\b\n",
    "\n",
    "        # Step2: Find roots of residual function\n",
    "        \n",
    "        eps3 = findmin([eps2,1e-4*abs(sol[end])])[1]\n",
    "        xroot = zeros(BigFloat,degree,1)\n",
    "        for i=1:degree\n",
    "            rootl = xapp[i]\n",
    "            rootr = xapp[i+1]\n",
    "            pm = (-1)^(i)*sign(sol[end])\n",
    "            froot(x) = chebyshevfunc(x,sol[1:end-1],parity,-targetf(x))\n",
    "            rootiter = 0\n",
    "            xroot[i] =  brent(rootl,rootr,froot,eps3,eps3)\n",
    "            if(xroot[i]==Inf)\n",
    "                return\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        # Step3: In each pair of adjacent roots, find a point x such that the absolute value of \n",
    "        #        residual function is maximized. In addition, values of residual function alternate\n",
    "        #        in sign.\n",
    "                 \n",
    "        xappnew = copy(xapp)\n",
    "        maxtot = abs(sol[end])\n",
    "        r_max = -Inf\n",
    "        y_max = -Inf\n",
    "        \n",
    "        for i=1:degree+1\n",
    "            maxapp = abs(sol[end])\n",
    "            pm = (-1)^(i)*sign(sol[end])\n",
    "            if(i==1)\n",
    "                lend = lef\n",
    "            else\n",
    "                lend = xroot[i-1]\n",
    "            end\n",
    "            if(i==degree+1)\n",
    "                rend = rig\n",
    "            else\n",
    "                rend = xroot[i]\n",
    "            end\n",
    "            exh = collect(range(lend,stop=rend,length=sample_size))\n",
    "            for j=1:length(exh)\n",
    "                fval = chebyshevfunc(exh[j],sol[1:end-1],parity,-targetf(exh[j]))\n",
    "                if(abs(fval)>r_max)\n",
    "                    r_max = abs(fval)\n",
    "                end\n",
    "                if(fval*pm>y_max)\n",
    "                    y_max = fval*pm\n",
    "                end\n",
    "                if(fval*pm>maxapp)\n",
    "                    maxapp = fval*pm\n",
    "                    maxtot = maximum([maxtot,maxapp])\n",
    "                    xappnew[i] = exh[j]\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        xapp = copy(xappnew)\n",
    "        \n",
    "        if(abs(r_max-y_max)>1e-12)\n",
    "            @printf(\"Warning: the interpolation points maybe incorrect\\n\")\n",
    "        end\n",
    "        @printf(\"The %3d-th itertion: previous error is %5.4e, L_inf approximation error is %5.4e\\n\",iter,sol[end],maxtot)\n",
    "        if(maxtot<eps||iter>=maxiter||abs((sol[end]-maxtot)/sol[end])<1e-4)\n",
    "            return sol[1:end-1]\n",
    "        end        \n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "chebyshevfunc (generic function with 1 method)"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function chebyshev(x,n) # T_n(x)\n",
    "    if(abs(x)<=1)\n",
    "        return cos(n*acos(x))\n",
    "    elseif(x>1)\n",
    "        return cosh(n*acosh(x))\n",
    "    else\n",
    "        return (-1)^n*cosh(n*acosh(-x))\n",
    "    end\n",
    "end\n",
    "\n",
    "function chebyshevfunc(x,sol,parity,init)\n",
    "    # Compute the value of a summation of Chebyshev polynomials at x\n",
    "    y = init\n",
    "    for i=1:length(sol)\n",
    "        if(parity==0)\n",
    "            deg = 2*(i-1)\n",
    "        elseif(parity==1)\n",
    "            deg = 2*i-1\n",
    "        else\n",
    "            deg = i-1\n",
    "        end\n",
    "        y += sol[i]*chebyshev(x,deg)\n",
    "    end\n",
    "    return y\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "brent (generic function with 1 method)"
      ]
     },
     "execution_count": 22,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function brent(a,b,f,tol1,tol2) \n",
    "    # brent method for finding roots on a given interval [a,b]\n",
    "    # f(a)f(b) should be less than 0, tol1, tol2 are stopping criteria\n",
    "    fa = f(a)\n",
    "    fb = f(b)\n",
    "    if(f(a)*f(b)>0)\n",
    "        println(\"Error, f(a)f(b)>0\")\n",
    "        return Inf\n",
    "    end\n",
    "    if(abs(f(a))<abs(f(b)))\n",
    "        tmp = a\n",
    "        tmp2 = fa\n",
    "        a = b\n",
    "        fa = fb\n",
    "        b = tmp\n",
    "        fb = tmp2\n",
    "    end\n",
    "    c = a\n",
    "    fc = fa\n",
    "    s = b\n",
    "    d = 1e-10\n",
    "    iter = 0\n",
    "    mflag = true\n",
    "    while(true)\n",
    "        iter += 1\n",
    "        if(iter>1000)\n",
    "            println(\"Brent method: reaches max iteration.\")\n",
    "            return Inf\n",
    "        end\n",
    "        if(abs(b-a)<tol2||abs(f(s))<tol1)\n",
    "            return s\n",
    "        end\n",
    "        if(fa!=fc&&fb!=fc)\n",
    "            s = a*fb*fc/((fa-fb)*(fa-fc))+b*fa*fc/((fb-fa)*(fb-fc))+c*fa*fb/((fc-fa)*(fc-fb))\n",
    "        else\n",
    "            s = b-fb*(b-a)/(fb-fa)\n",
    "        end\n",
    "        if((s>=b)||(s<=(3*a+b)/4)||((abs(s-b)*2>abs(b-c))&&mflag)||((abs(s-b)*2>abs(d-c))&&(!mflag))||(tol1>abs(b-c)&&mflag)||((tol1>abs(d-c))&&(!mflag)))\n",
    "            s = (a+b)/2\n",
    "            mflag = true\n",
    "        else\n",
    "            mflag = false\n",
    "        end\n",
    "        fs = f(s)\n",
    "        d = c\n",
    "        c = b\n",
    "        fc = fb\n",
    "        if(fa*fs<0)\n",
    "            b = s\n",
    "            fb = fs\n",
    "        else\n",
    "            a = s\n",
    "            fa = fs\n",
    "        end\n",
    "        if(abs(f(a))<abs(f(b)))\n",
    "            tmp = a\n",
    "            tmp2 = fa\n",
    "            a = b\n",
    "            fa = fb\n",
    "            b = tmp\n",
    "            fb = tmp2\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 23,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "The   1-th itertion: previous error is 1.9048e-02, L_inf approximation error is 2.4882e-02\n",
      "The   2-th itertion: previous error is 2.2234e-02, L_inf approximation error is 2.2325e-02\n",
      "The   3-th itertion: previous error is 2.2281e-02, L_inf approximation error is 2.2282e-02\n"
     ]
    }
   ],
   "source": [
    "# Test case: Matrix inversion\n",
    "#\n",
    "# We would like to approximate f = g \\circ h over [1/kappa,1] by a polynomial (even or odd)\n",
    "#\n",
    "# You may change the target function to approximate desired function, and you can\n",
    "# save coefficients of approximation polynomail in .mat file and solve for corresponding\n",
    "# phase factors via optimization method.\n",
    "#\n",
    "# parameters\n",
    "#     kappa: parameters of polynomial approximation\n",
    "#     degree: degree of freedom of approximation polynomial (not the degree)\n",
    "#     stop_eps: desired accuracy\n",
    "#     parity: parity of the approximation polynomial\n",
    "#     R_high: number of bits sued in high-precision arithmetic\n",
    "#     save_mat: whether to save the coefficient as a file (.mat)\n",
    "#     where_save: path to save the data file\n",
    "#     save_name: name of the data file\n",
    "#\n",
    "# Authors: Y Dong, L Lin\n",
    "# Last revision: 06/2020\n",
    "\n",
    "\n",
    "kappa = 5\n",
    "degree = 4\n",
    "stop_eps = 1e-3\n",
    "parity = 0\n",
    "if parity == 0\n",
    "    order = 2*(degree-1)\n",
    "else\n",
    "    order = 2*degree-1\n",
    "end\n",
    "R_high = 512\n",
    "save_mat = true\n",
    "where_save = \"./\"\n",
    "save_name = \"coef_\" * string(kappa) *\"_\"*string(order)\n",
    "\n",
    "#------------------------------------------------------------------\n",
    "\n",
    "function inversex(x) #1/x divided by a constant factor\n",
    "    return big.(1)/(kappa*( (1-1/kappa)*x^2 + 1/kappa ))\n",
    "end\n",
    "\n",
    "setprecision(BigFloat,R_high)\n",
    "xapp = big.(cos.(collect(range(pi/2,stop=0,length=degree+1))))\n",
    "solu = Remez(inversex, parity, degree, xapp, 0.0, 1.0, 20, 20, stop_eps)\n",
    "solu = Float64.(solu)\n",
    "\n",
    "if(save_mat)\n",
    "    matpath = where_save\n",
    "    if(save_name!=\"\")\n",
    "        mattest = matopen(matpath * save_name * \".mat\",\"w\")\n",
    "    else\n",
    "        mattest = matopen(matpath * \"coef_x_\" * string(kappa) * \"_\" * string(ceil(Int,-log10(stop_eps))) * \".mat\",\"w\")\n",
    "    end\n",
    "    write(mattest,\"coef\",Float64.(solu))\n",
    "    write(mattest,\"parity\",parity)\n",
    "    close(mattest)\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
